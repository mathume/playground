version '1.0-SNAPSHOT'
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
    }
}

apply plugin: 'java'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    compile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

// SONARQUBE
////////////

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", jacoco.reportsDir
        property "sonar.junit.reportsPath", testResultsDir
    }
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"

// CUCUMBER
////////////

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin',
                    'pretty',
                    '--plugin',
                    'junit:build/cucumber/cucumber-junit-report.xml',
                    '--plugin',
                    'html:build/cucumber/html',
                    '--plugin',
                    'rerun:build/cucumber/rerun.txt',
                    '--glue', 'com.mathume.playground.cucumber',
                    '--tags', '~@skip',
                    '.']
        }
    }
}

task cucumberRerun() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin',
                    'pretty',
                    '--plugin',
                    'junit:build/cucumber/rerun/cucumber-junit-report.xml',
                    '--plugin',
                    'html:build/cucumber/rerun/html',
                    '--glue', 'es.mediatechsolutions.gameserver.cucumber',
                    '@build/cucumber/rerun.txt']
        }
    }
}
